import "root:/"
import "root:/modules/common"
import "root:/modules/common/widgets"
import "root:/services"
import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import Qt5Compat.GraphicalEffects
import Quickshell
import Quickshell.Wayland
import Quickshell.Hyprland
import Quickshell.Io
import Quickshell.Services.Mpris

Scope {
    id: bar

    readonly property int barHeight: Appearance.sizes.barHeight
    readonly property int barCenterSideModuleWidth: Appearance.sizes.barCenterSideModuleWidth
    readonly property int osdHideMouseMoveThreshold: 20
    property bool showBarBackground: ConfigOptions.bar.showBackground

    // Watch for changes in blur settings
    Connections {
        target: AppearanceSettingsState
        function onBarBlurAmountChanged() {
            // Update Hyprland blur rules for bar
            Hyprland.dispatch(`keyword decoration:blur:passes ${AppearanceSettingsState.barBlurPasses}`)
            Hyprland.dispatch(`keyword decoration:blur:size ${AppearanceSettingsState.barBlurAmount}`)
            Hyprland.dispatch(`keyword layerrule blur,^(quickshell:bar:blur)$`)
            // Reload Quickshell
            Hyprland.dispatch("exec killall -SIGUSR2 quickshell")
        }
        function onBarBlurPassesChanged() {
            Hyprland.dispatch(`keyword decoration:blur:passes ${AppearanceSettingsState.barBlurPasses}`)
            Hyprland.dispatch(`keyword layerrule blur,^(quickshell:bar:blur)$`)
            // Reload Quickshell
            Hyprland.dispatch("exec killall -SIGUSR2 quickshell")
        }
    }

    Component.onCompleted: {
        // Apply initial blur settings
        Hyprland.dispatch(`keyword decoration:blur:passes ${AppearanceSettingsState.barBlurPasses}`)
        Hyprland.dispatch(`keyword decoration:blur:size ${AppearanceSettingsState.barBlurAmount}`)
        Hyprland.dispatch(`keyword layerrule blur,^(quickshell:bar:blur)$`)
    }

    Variants { // For each monitor
        model: Quickshell.screens

        PanelWindow { // Bar window
            id: barRoot

            property ShellScreen modelData
            property var brightnessMonitor: Brightness.getMonitorForScreen(modelData)
            property real useShortenedForm: (Appearance.sizes.barHellaShortenScreenWidthThreshold >= screen.width) ? 2 :
                (Appearance.sizes.barShortenScreenWidthThreshold >= screen.width) ? 1 : 0
            readonly property int centerSideModuleWidth: 
                (useShortenedForm == 2) ? Appearance.sizes.barCenterSideModuleWidthHellaShortened :
                (useShortenedForm == 1) ? Appearance.sizes.barCenterSideModuleWidthShortened : 
                    Appearance.sizes.barCenterSideModuleWidth

            // NetworkTooltip {
            //     id: networkTooltip
            //     screen: modelData
            // }

            screen: modelData
            implicitHeight: barHeight
            exclusiveZone: showBarBackground ? barHeight : (barHeight - 4)
            mask: Region {
                item: barContent
            }
            color: "transparent"
            WlrLayershell.namespace: "quickshell:bar:blur"

            anchors {
                top: true
                left: true
                right: true
            }

            Rectangle { // Bar background
                id: barContent
                anchors.right: parent.right
                anchors.left: parent.left
                anchors.top: parent.top
                height: barHeight
                    color: showBarBackground ? Qt.rgba(
                        Appearance.colors.colLayer0.r,
                        Appearance.colors.colLayer0.g,
                        Appearance.colors.colLayer0.b,
                    0.55
                    ) : "transparent"
                    
                    Behavior on color {
                        ColorAnimation {
                            duration: Appearance.animation.elementMoveFast.duration
                            easing.type: Appearance.animation.elementMoveFast.type
                    }
                }

                // Bottom border
                Rectangle {
                    anchors.left: parent.left
                    anchors.right: parent.right
                    anchors.bottom: parent.bottom
                    height: 3
                    color: "black"
                    opacity: 0.35
                }
                
                MouseArea { // Left side | scroll to change brightness
                    id: barLeftSideMouseArea
                    anchors.left: parent.left
                    implicitHeight: barHeight
                    width: (barRoot.width - middleSection.width) / 2
                    property bool hovered: false
                    property real lastScrollX: 0
                    property real lastScrollY: 1
                    property bool trackingScroll: false
                    acceptedButtons: Qt.NoButton  // Disable click handling for the large area
                    hoverEnabled: false
                    propagateComposedEvents: true
                    
                    // Scroll to change brightness
                    WheelHandler {
                        onWheel: (event) => {
                            if (event.angleDelta.y < 0)
                                barRoot.brightnessMonitor.setBrightness(barRoot.brightnessMonitor.brightness - 0.05);
                            else if (event.angleDelta.y > 0)
                                barRoot.brightnessMonitor.setBrightness(barRoot.brightnessMonitor.brightness + 0.05);
                            // Store the mouse position and start tracking
                            barLeftSideMouseArea.lastScrollX = event.x;
                            barLeftSideMouseArea.lastScrollY = event.y;
                            barLeftSideMouseArea.trackingScroll = true;
                        }
                        acceptedDevices: PointerDevice.Mouse | PointerDevice.TouchPad
                    }
                    onPositionChanged: (mouse) => {
                        if (barLeftSideMouseArea.trackingScroll) {
                            const dx = mouse.x - barLeftSideMouseArea.lastScrollX;
                            const dy = mouse.y - barLeftSideMouseArea.lastScrollY;
                            if (Math.sqrt(dx*dx + dy*dy) > osdHideMouseMoveThreshold) {
                                Hyprland.dispatch('global quickshell:osdBrightnessHide')
                                barLeftSideMouseArea.trackingScroll = false;
                            }
                        }
                    }
                    Item {  // Left section
                        anchors.fill: parent
                        implicitHeight: leftSectionRowLayout.implicitHeight
                        implicitWidth: leftSectionRowLayout.implicitWidth
                        
                        RowLayout { // Content
                            id: leftSectionRowLayout
                            anchors.fill: parent
                            spacing: 10

                            Rectangle {
                                id: archLogoContainer
                                Layout.alignment: Qt.AlignLeft | Qt.AlignVCenter
                                Layout.leftMargin: 2
                                Layout.fillWidth: false
                                
                                radius: Appearance.rounding.full
                                color: archMouseArea.containsMouse ? 
                                    Qt.rgba(Appearance.colors.colLayer1Active.r, Appearance.colors.colLayer1Active.g, Appearance.colors.colLayer1Active.b, 0.8) : 
                                    "transparent"
                                implicitWidth: archLogo.width + 10
                                implicitHeight: archLogo.height + 10

                                Image {
                                    id: archLogo
                                    anchors.centerIn: parent
                                    width: 22
                                    height: 22
                                    source: "file://$HOME/.config/quickshell/logo/Arch-linux-logo.png"
                                    fillMode: Image.PreserveAspectFit
                                }
                                
                                // Mouse area only for the Arch logo
                                MouseArea {
                                    id: archMouseArea
                                    anchors.fill: parent
                                    hoverEnabled: true
                                    acceptedButtons: Qt.LeftButton
                                    
                                    onClicked: {
                                        GlobalStates.hyprMenuOpen = !GlobalStates.hyprMenuOpen
                                    }
                                    
                                    onPressed: (event) => {
                                        if (event.button === Qt.LeftButton) {
                                            Hyprland.dispatch('global quickshell:sidebarLeftOpen')
                                        }
                                    }
                                }
                            }

                            // Spacer
                            Item {
                                Layout.fillWidth: true
                            }
                        }
                    }
                }

                RowLayout { // Middle section
                    id: middleSection
                    anchors.centerIn: parent
                    spacing: 8

                    RowLayout {
                        id: leftCenterGroup
                        Layout.fillWidth: true
                        Layout.fillHeight: true
                        // (empty, reserved for future symmetry)
                    }

                    RowLayout {
                        id: middleCenterGroup
                        Layout.fillWidth: true
                        Layout.fillHeight: true
                        Workspaces {
                            bar: barRoot
                            Layout.alignment: Qt.AlignCenter
                            MouseArea { // Right-click to toggle overview
                                anchors.fill: parent
                                acceptedButtons: Qt.RightButton
                                onPressed: (event) => {
                                    if (event.button === Qt.RightButton) {
                                        Hyprland.dispatch('global quickshell:overviewToggle')
                                    }
                                }
                            }
                        }
                    }

                    RowLayout {
                        id: rightCenterGroup
                        Layout.fillWidth: true
                        Layout.fillHeight: true
                        // (empty, reserved for future symmetry)
                    }
                }

                MouseArea { // Right side | scroll to change volume
                    id: barRightSideMouseArea

                    anchors.right: parent.right
                    implicitHeight: barHeight
                    width: (barRoot.width - middleSection.width) / 2

                    property bool hovered: false
                    property real lastScrollX: 0
                    property real lastScrollY: 0
                    property bool trackingScroll: false
                    
                    acceptedButtons: Qt.NoButton  // Disable click handling for the large area
                    hoverEnabled: false
                    propagateComposedEvents: true

                    // Scroll to change volume
                    WheelHandler {
                        onWheel: (event) => {
                            const currentVolume = Audio.value;
                            const step = currentVolume < 0.1 ? 0.01 : 0.02 || 0.2;
                            if (event.angleDelta.y < 0)
                                Audio.sink.audio.volume -= step;
                            else if (event.angleDelta.y > 0)
                                Audio.sink.audio.volume = Math.min(1, Audio.sink.audio.volume + step);
                            // Store the mouse position and start tracking
                            barRightSideMouseArea.lastScrollX = event.x;
                            barRightSideMouseArea.lastScrollY = event.y;
                            barRightSideMouseArea.trackingScroll = true;
                        }
                        acceptedDevices: PointerDevice.Mouse | PointerDevice.TouchPad
                    }
                    onPositionChanged: (mouse) => {
                        if (barRightSideMouseArea.trackingScroll) {
                            const dx = mouse.x - barRightSideMouseArea.lastScrollX;
                            const dy = mouse.y - barRightSideMouseArea.lastScrollY;
                            if (Math.sqrt(dx*dx + dy*dy) > osdHideMouseMoveThreshold) {
                                Hyprland.dispatch('global quickshell:osdVolumeHide')
                                barRightSideMouseArea.trackingScroll = false;
                            }
                        }
                    }

                    Item {
                        anchors.fill: parent
                        implicitHeight: rightSectionRowLayout.implicitHeight
                        implicitWidth: rightSectionRowLayout.implicitWidth

                        RowLayout {
                            id: rightSectionRowLayout
                            anchors.fill: parent
                            spacing: 8
                            layoutDirection: Qt.RightToLeft
                    
                            // System indicators with its own mouse area
                            Rectangle {
                                id: indicatorsContainer
                                Layout.margins: 4
                                Layout.rightMargin: 2
                                Layout.fillHeight: true
                                implicitWidth: indicatorsRowLayout.implicitWidth + 20
                                radius: Appearance.rounding.full
                                color: (indicatorsMouseArea.pressed || GlobalStates.sidebarRightOpen) ? 
                                    Qt.rgba(Appearance.colors.colLayer1Active.r, Appearance.colors.colLayer1Active.g, Appearance.colors.colLayer1Active.b, 0.8) : 
                                    indicatorsMouseArea.hovered ? 
                                    Qt.rgba(Appearance.colors.colLayer1Hover.r, Appearance.colors.colLayer1Hover.g, Appearance.colors.colLayer1Hover.b, 0.8) : 
                                    "transparent"
                                
                                // Mouse area only for the indicators section
                                MouseArea {
                                    id: indicatorsMouseArea
                                    anchors.fill: parent
                                    acceptedButtons: Qt.LeftButton
                                    hoverEnabled: true
                                    
                                    onPressed: (event) => {
                                        if (event.button === Qt.LeftButton) {
                                            Hyprland.dispatch('global quickshell:sidebarRightOpen')
                                        }
                                    }
                                }
                                
                                RowLayout {
                                    id: indicatorsRowLayout
                                    anchors.centerIn: parent
                                    property real realSpacing: 15
                                    spacing: 0
                                    
                                    Revealer {
                                        reveal: Audio.sink?.audio?.muted ?? false
                                        Layout.fillHeight: true
                                        Layout.rightMargin: reveal ? indicatorsRowLayout.realSpacing : 0
                                        Behavior on Layout.rightMargin {
                                            NumberAnimation {
                                                duration: Appearance.animation.elementMoveFast.duration
                                                easing.type: Appearance.animation.elementMoveFast.type
                                                easing.bezierCurve: Appearance.animation.elementMoveFast.bezierCurve
                                            }
                                        }
                                        MaterialSymbol {
                                            text: "volume_off"
                                            iconSize: Appearance.font.pixelSize.larger
                                            color: Appearance.colors.colOnLayer0
                                        }
                                    }
                                    Revealer {
                                        reveal: Audio.source?.audio?.muted ?? false
                                        Layout.fillHeight: true
                                        Layout.rightMargin: reveal ? indicatorsRowLayout.realSpacing : 0
                                        Behavior on Layout.rightMargin {
                                            NumberAnimation {
                                                duration: Appearance.animation.elementMoveFast.duration
                                                easing.type: Appearance.animation.elementMoveFast.type
                                                easing.bezierCurve: Appearance.animation.elementMoveFast.bezierCurve
                                            }
                                        }
                                        MaterialSymbol {
                                            text: "mic_off"
                                            iconSize: Appearance.font.pixelSize.larger
                                            color: Appearance.colors.colOnLayer0
                                        }
                                    }
                                    // Network icons
                                    Item {
                                        width: Appearance.font.pixelSize.larger
                                        height: Appearance.font.pixelSize.larger
                                        Layout.rightMargin: indicatorsRowLayout.realSpacing - 1.5
                                        
                                        RowLayout {
                                            anchors.fill: parent
                                            spacing: 4

                                            // Ethernet icon
                                            Rectangle {
                                                Layout.preferredWidth: Appearance.font.pixelSize.larger * 0.85
                                                Layout.preferredHeight: Appearance.font.pixelSize.larger * 0.85
                                                color: "transparent"
                                                visible: Network.networkType === "ethernet"

                                                AnimatedEthernetIcon {
                                                    anchors.fill: parent
                                                    iconSize: parent.width
                                                    iconColor: Appearance.colors.colOnLayer0
                                                }
                                            }

                                            // WiFi icon
                                            Rectangle {
                                                id: wifiIconRect
                                                Layout.preferredWidth: Appearance.font.pixelSize.larger * 0.85
                                                Layout.preferredHeight: Appearance.font.pixelSize.larger * 0.85
                                                color: "transparent"
                                                visible: Network.networkType === "wifi"

                                                SystemIcon {
                                                    id: wifiIcon
                                                    anchors.fill: parent
                                                    iconName: Network.wifiEnabled ? (
                                                        Network.networkStrength >= 90 ? "network-wireless-signal-excellent" :
                                                        Network.networkStrength >= 80 ? "network-wireless-signal-good" :
                                                        Network.networkStrength >= 65 ? "network-wireless-signal-ok" :
                                                        Network.networkStrength >= 45 ? "network-wireless-signal-weak" :
                                                        Network.networkStrength >= 25 ? "network-wireless-signal-none" :
                                                        Network.networkStrength >= 10 ? "network-wireless-signal-none" :
                                                        "network-wireless-signal-none"
                                                    ) : "network-wireless-offline"
                                                    iconSize: parent.width
                                                    iconColor: Appearance.colors.colOnLayer0
                                                    fallbackIcon: "network-wireless"
                                                    opacity: Network.wifiEnabled ? 1.0 : 0.5
                                                
                                                    Behavior on iconName {
                                                        PropertyAnimation {
                                                        duration: Appearance.animation.elementMoveFast.duration
                                                        easing.type: Appearance.animation.elementMoveFast.type
                                                    }
                                                }
                                                }

                                                MouseArea {
                                                    anchors.fill: parent
                                                    hoverEnabled: false
                                                    // Tooltip disabled to prevent crashes
                                                    // onEntered: networkTooltip.show()
                                                    // onExited: networkTooltip.hide()
                                                    // onPositionChanged: (mouse) => {
                                                    //     var point = wifiIconRect.mapToItem(null, mouse.x, mouse.y)
                                                    //     networkTooltip.updatePosition(point.x, point.y)
                                                    // }
                                                }
                                            }
                                        }
                                    }
                                    MaterialSymbol {
                                        text: Bluetooth.bluetoothConnected ? "bluetooth_connected" : Bluetooth.bluetoothEnabled ? "bluetooth" : "bluetooth_disabled"
                                        iconSize: Appearance.font.pixelSize.larger
                                        color: Appearance.colors.colOnLayer0
                                    }
                                }
                            }

                            ClockWidget {
                                Layout.alignment: Qt.AlignVCenter
                                Layout.rightMargin: 4
                                Layout.leftMargin: 4
                            }

                            Weather {
                                Layout.alignment: Qt.AlignVCenter
                                Layout.rightMargin: 4
                                Layout.leftMargin: 4
                                Layout.minimumWidth: 100
                                weatherLocation: "Halifax, Nova Scotia, Canada"
                            }

                            SysTray {
                                bar: barRoot
                                Layout.fillWidth: false
                                Layout.fillHeight: true
                                Layout.rightMargin: 4
                                Layout.leftMargin: 4
                            }

                            Item {
                                Layout.fillWidth: true
                                Layout.fillHeight: true
                            }
                        }
                    }
                }
            }

            // Round decorators
            Item {
                anchors.left: parent.left
                anchors.right: parent.right
                anchors.top: barContent.bottom
                height: 0

                RoundCorner {
                    anchors.top: parent.top
                    anchors.left: parent.left
                    size: showBarBackground ? Appearance.rounding.small : 1
                    corner: cornerEnum.topLeft
                    color: showBarBackground ? Appearance.colors.colLayer0 : "transparent"
                    visible: showBarBackground
                }
                RoundCorner {
                    anchors.top: parent.top
                    anchors.right: parent.right
                    size: showBarBackground ? Appearance.rounding.small : 1
                    corner: cornerEnum.topRight
                    color: showBarBackground ? Appearance.colors.colLayer0 : "transparent"
                    visible: showBarBackground
                }
            }

        }

    }

}
